expression = assignment ;
assignment = IDENTIFIER ( "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | "^=" ) logic_or | logic_or ;
logic_or = logic_and { "||" logic_and } ;
logic_and = equality { "&&" equality } ;
equality = comparison { ( "==" | "!=" ) comparison } ;
comparison = shift { ( ">" | ">=" | "<" | "<=" ) shift } ;
shift = term { ( "<<" | ">>" ) term } ;
term = factor { ( "+" | "-" ) factor } ;
factor = unary { ( "*" | "/" ) unary } ;
unary = ( "!" | "-" ) unary | member_access ;
member_access = primary { "." IDENTIFIER } ;
primary = STRING | IDENTIFIER | NUMBER | "true" | "false" | "(" expression ")" | "(" ")" | block | conditional ;
block = "{" expression "}" ;
conditional = "if" expression block { "else" "if" expression block } [ "else" block ] ;
